plugins {
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'scala'
apply plugin: 'java-library'

group 'com.sbuslab'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/public' }
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
        publications {
        mavenJava(MavenPublication) {
            artifactId = 'sbus'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'sbus'
                description = 'Multi-transport service bus'
                url = 'https://github.com/sbuslab/sbus'
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'kulikovd'
                        name = 'Dmitry Kulikov'
                        email = 'kulikov.dm@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/sbuslab/sbus'
                    connection = 'scm:https://github.com/sbuslab/sbus.git'
                    developerConnection = 'scm:git@github.com:sbuslab/sbus.git'
                }
            }
        }
    }

    if (project.findProperty('publishSonatype')) {
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"

                credentials {
                    username = project.findProperty('nexusUsername') ?: ''
                    password = project.findProperty('nexusPassword') ?: ''
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    api 'com.sbuslab:model:[1.1.0,1.2.0)'

    api 'com.typesafe.akka:akka-actor_2.12:2.6.19'
    implementation 'com.typesafe.akka:akka-slf4j_2.12:2.6.19'
    api 'com.typesafe.scala-logging:scala-logging_2.12:3.9.5'

    implementation 'com.rabbitmq:amqp-client:5.14.2'
    implementation 'eu.shiftforward:amqp-client_2.12:1.6.6'

    implementation 'io.prometheus:simpleclient:0.15.0'

    implementation 'net.i2p.crypto:eddsa:0.3.0'

    implementation 'com.fasterxml.jackson.module:jackson-module-scala_2.12:2.13.4'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.scalatest:scalatest_2.12:3.0.5'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.springframework:spring-test:5.3.19'
    testImplementation 'com.github.tomakehurst:wiremock:2.27.2'
    testImplementation 'commons-codec:commons-codec:1.15'
}
